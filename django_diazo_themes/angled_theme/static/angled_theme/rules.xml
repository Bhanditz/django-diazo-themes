<?xml version="1.0" encoding="UTF-8"?>
<rules
    xmlns="http://namespaces.plone.org/diazo"
    xmlns:css="http://namespaces.plone.org/diazo/css"
    xmlns:xsl="http://www.w3.org/1999/XSL/Transform">

    <!-- Nifty output settings -->
    <xsl:strip-space elements="*"/>
    <xsl:output method="xml" indent="yes" omit-xml-declaration="yes"/>

    <!-- Identify Django error page -->
    <xsl:variable name="django_error" select="//body/div[@id='summary'] and //body/div[@id='traceback'] and
    //body/div[@id='requestinfo'] and //body/div[@id='explanation']" />

    <!-- Skip Diazo -->
    <notheme if-path="admin" /><!-- Django Admin -->
    <notheme if="$django_error" /><!-- Django Error -->

    <!-- Theme to use -->
    <theme href="index.html" />

    <!-- Title of the page -->
    <replace css:theme-children="title"
             css:content-children="context > title" />

    <!-- Brandname + link to home -->
    <replace css:theme="#logo h1 a">
        <xsl:element name="a">
            <xsl:attribute name="href">
                <xsl:value-of select="//context/brand/url" />
            </xsl:attribute>
            <xsl:value-of select="//context/brand/name" />
        </xsl:element>
    </replace>

    <!-- Menu -->
    <replace css:theme-children="#menu ul">
        <xsl:for-each css:select="context menu item">
            <xsl:element name="li">
                <xsl:if test="./@class='active'">
                    <xsl:attribute name="class">current_page_item</xsl:attribute>
                </xsl:if>
                <xsl:element name="a">
                    <xsl:attribute name="href">
                        <xsl:value-of select="./url" />
                    </xsl:attribute>
                    <xsl:value-of select="./name" />
                </xsl:element>
            </xsl:element>
        </xsl:for-each>
    </replace>

    <!-- Main content -->
    <replace css:theme-children="#wide-content">
        <h2><xsl:value-of css:select="context > title" /></h2>
        <xsl:copy-of select="//context/content/*" />
    </replace>

    <!-- News -->
    <replace css:theme-children="#content">
        <ul class="style1">
            <xsl:for-each css:select="context news newsitems item">
                <xsl:if test="not(position() > 4)">
                    <xsl:element name="li">
                        <xsl:if test="position() = 1">
                            <xsl:attribute name="class">first</xsl:attribute>
                        </xsl:if>
                        <h3><xsl:value-of select="./title" /></h3>
                        <p><xsl:value-of select="./text" /></p>
                        <p>
                            <xsl:element name="a">
                                <xsl:attribute name="class">button-style</xsl:attribute>
                                <xsl:attribute name="href"><xsl:value-of select="./button/url" /></xsl:attribute>
                                <xsl:value-of select="./button/name" />
                            </xsl:element>
                        </p>
                    </xsl:element>
                </xsl:if>
            </xsl:for-each>
        </ul>
    </replace>

    <!-- LHS portlet -->
    <replace css:theme-children="#sidebar">
        <h2><xsl:value-of css:select="context rhs_portlet title" /></h2>
        <ul class="style3">
            <xsl:for-each css:select="context rhs_portlet content item">
                <xsl:if test="not(position() > 4)">
                    <xsl:element name="li">
                        <xsl:if test="position() = 1">
                            <xsl:attribute name="class">first</xsl:attribute>
                        </xsl:if>
                        <p class="date"><xsl:value-of select="./posted/date" /></p>
                        <p><xsl:value-of select="./text" /></p>
                    </xsl:element>
                </xsl:if>
            </xsl:for-each>
        </ul>
    </replace>

    <!-- Footer -->
    <replace css:theme-children="#footer"
             css:content-children="footer" />

</rules>
